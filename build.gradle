apply plugin: 'application'

group 'org.comroid'
version '0.01-SNAPSHOT'

apply from: 'gradle/vars.gradle'

mainClassName = 'de.kaleidox.kram.chat.ChatServer'

sourceCompatibility = 11
targetCompatibility = 11

tasks.findByName('wrapper')?.configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
}

if (!isAutoDeploy) {
    evaluationDependsOn ':guardian:uniform'
    evaluationDependsOn ':guardian:restless'
}

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'

    if (isAutoDeploy) {
        implementation 'org.comroid:webkit:0.+'
    } else {
        implementation project(':guardian:uniform')
        implementation project(':guardian:restless')
        implementation project(':guardian:webkit')
    }

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.11.0'

    // runtime adapter dependencies
    implementation 'com.alibaba:fastjson:1.2.70'
}
